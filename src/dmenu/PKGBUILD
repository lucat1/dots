pkgname=dmenu
pkgver=4.9
pkgrel=1
pkgdesc='Generic menu for X'
url='https://tools.suckless.org/dmenu/'
arch=('x86_64')
license=('MIT')
depends=('sh' 'libxinerama' 'libxft' 'freetype2')
source=("https://dl.suckless.org/tools/dmenu-${pkgver}.tar.gz" "dmenu.c" "config.h")

prepare() {
  cd ${pkgname}-${pkgver}
  cp ../config.h .
  cp ../dmenu.c .
  echo "CPPFLAGS+=${CPPFLAGS}" >> config.mk
  echo "CFLAGS+=${CFLAGS}" >> config.mk
  echo "LDFLAGS+=${LDFLAGS}" >> config.mk
}

build() {
  cd ${pkgname}-${pkgver}
  make \
	  X11INC=/usr/include/X11 \
	  X11LIB=/usr/lib/X11 \
	  FREETYPEINC=/usr/include/freetype2
}

package() {
  cd ${pkgname}-${pkgver}
  make PREFIX=/usr DESTDIR="${pkgdir}" install
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
md5sums=('9a537ec9a3a2ce9f08963d66b56cc030'
         '26038c052d3d321f750b3eed96299742'
         '933ac2426be1f984559acb9e111a67ce')
