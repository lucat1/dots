#!/usr/bin/fish
# vim: set syntax=sh:
# screenshot/screencaputre utility

set folder "$HOME/Pictures/screens"
set file "$folder/scren-%s"

function screenshot
  set geometry (printf "%dx%d+%d+%d" $argv[3] $argv[4] $argv[1] $argv[2])
  import -w root -crop $geometry $file
end

function screencapture
  set -l size "$argv[3]x$argv[4]"
  set -l offset "$DISPLAY.0+$argv[1],$argv[2]"
  ffmpeg -y -loglevel debug \
    -f x11grab \
    -s $size \
    -i $offset \
    -framerate 30 \
    -threads (nproc --all) \
    $file &
end

# main()
argparse --name=kap 'p/picture' 'v/video' -- $argv 
or exit 1

if test -z "$_flag_p" && test -z "$_flag_v"
  echo "usage: kap [-p] [-v]"
  echo "you must select either [p]icture or [v]ideo mode"
  exit 1
end

# create the screenshot directory if it doesn't exist
if ! test -d $folder 
  mkdir -p $folder
end

# grab dimentions
command slop -f "%x %y %w %h" | read -a -g dimentions -d ' '

# generate filename
set file (printf "$file" (date +"%Y-%d-%m-%H-%M-%S"))

if test -n "$_flag_p"
  # append extension to file path
  set file "$file.jpg"
  screenshot $dimentions
end

if test -n "$_flag_v"
  set file "$file.mp4"
  # start screen capture
  screencapture $dimentions

  # wait until the notification has been clicked to stop recording
  herbe 'click to stop recording' && killall ffmpeg # end the recording
end
 
if ! test -f $file 
  herbe "the screen wans't written"
  exit 1
end

set size (du -sh $file | awk '{print $1;}'" ")
herbe "saved $size" 
